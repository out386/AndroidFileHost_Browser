buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.22.0'
    }
}
apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'com.jakewharton.hugo'

repositories {
    maven { url 'https://maven.fabric.io/public' }
}


android {
    compileSdkVersion 26
    buildToolsVersion "26.0.0"

    lintOptions {
        abortOnError false
    }

    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            output.outputFile = new File(
                    output.outputFile.parent, "afh-browser_beta_"+versionCode+".apk")
        }
    }

    defaultConfig {
        applicationId "browser.afh"
        minSdkVersion 21
        targetSdkVersion 26
        versionCode 11
        versionName "1.0.4"
    }

    signingConfigs {
        release {
            storeFile file("../extras/keystore/keystore.jks")
            storePassword System.getenv("KEYSTORE_PASSWD")
            keyAlias "stalker"
            keyPassword System.getenv("KEY_PASSWD")
         }
    }
    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled false
            shrinkResources true
            buildConfigField("boolean", "PLAY_COMPATIBLE", "false")
        }
        debug {
            debuggable true
            minifyEnabled false
            shrinkResources false
            buildConfigField("boolean", "PLAY_COMPATIBLE", "false")
        }
    }
}

dependencies {

    // support libraries
    compile 'com.android.support:cardview-v7:25.3.1'
    compile 'com.android.support:design:25.3.1'
    compile 'com.android.support:appcompat-v7:25.3.1'
    compile 'com.android.support:support-v4:25.3.1'
    compile 'com.android.support:recyclerview-v7:25.3.1'
    compile 'com.android.support:customtabs:25.3.1'

    // networking
    compile 'com.squareup.retrofit2:retrofit:2.0.2'
    compile 'com.squareup.okhttp3:logging-interceptor:3.5.0'
    compile 'com.squareup.okhttp3:okhttp:3.5.0'
    compile 'com.github.bumptech.glide:glide:3.7.0'
    compile 'com.github.bumptech.glide:okhttp3-integration:1.4.0@aar'

    // network response parsing
    compile 'com.google.code.gson:gson:2.7'
    compile 'com.squareup.retrofit2:converter-gson:2.0.2'

    // ui
    compile 'com.baoyz.pullrefreshlayout:library:1.2.0'
    compile('com.mikepenz:materialdrawer:5.8.1@aar') {
        transitive = true
    }
    compile('com.mikepenz:fastadapter:2.1.5@aar') {
        transitive = true
    }
    compile 'com.mikepenz:fastadapter-commons:2.1.0@aar'
    compile 'com.mikepenz.thirdparty:stickyheadersrecyclerview:0.4.4-SNAPSHOT@aar'
    compile 'com.turingtechnologies.materialscrollbar:lib:10.0.1'
    compile 'com.lapism:searchview:4.0'

    // dialogs
    compile 'com.github.javiersantos:BottomDialogs:1.2.1'
    compile 'com.afollestad.material-dialogs:core:0.9.1.0'

    // crashlytics
    debugCompile('com.crashlytics.sdk.android:crashlytics:2.6.5@aar') {
        transitive = true;
    }
    releaseCompile('com.crashlytics.sdk.android:crashlytics:2.6.5@aar') {
        transitive = true;
    }

    // updates
    compile 'com.github.javiersantos:AppUpdater:2.5'

    // the cool stuff
    compile('com.mikepenz:aboutlibraries:5.9.1@aar') {
        transitive = true
    }
    compile 'com.github.daniel-stoneuk:material-about-library:1.1.1'
    compile 'com.mikepenz:itemanimators:0.5.0@aar'
}

